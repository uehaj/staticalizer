// -*- groovy -*-

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'groovy'
apply plugin: 'maven'

defaultTasks 'build'

repositories { mavenCentral() }

group = 'org.jggug.kobo'
archivesBaseName = 'staticalizer'
version = '0.1'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.0.0'
    compile "commons-cli:commons-cli:1.2"
    groovy 'org.codehaus.groovy:groovy:2.0.0'
    testCompile 'junit:junit:4.7'
}

test.dependsOn jar

task writeNewPom << {
    pom {
        project {
            inceptionYear '2012'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("$buildDir/newpom.xml")
}

configurations {
    deployerJars
}

uploadArchives {
    repositories.mavenDeployer {
        name = 'sshDeployer' // optional
        configuration = configurations.deployerJars
        repository(url: "scp://repos.mycompany.com/releases") {
            authentication(userName: "me", password: "myPassword")
        }
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }        
    }
}

task dist(dependsOn: ['distBin', 'distSrc'])

def executablesSpec = copySpec {
    into('lib') {
        from "$buildDir/libs"
    }
    into('bin') {
        fileMode = 0755
        from "$projectDir/src/main/bin"
        filter(ReplaceTokens, tokens: ["STATICALIZER_VERSION": project.version])
    }
}

def srcSpec = copySpec {
    from("$projectDir") {
        include "build.gradle"
        include "gradle.properties"
    }
    into("src") {
        from "$projectDir/src"
    }
    from("$projectDir") {
        fileMode = 0755
        include "gradlew*"
    }
    into("gradle") {
        from "$projectDir/gradle"
    }
}

task distBin(type: Zip, dependsOn: 'jar') {
    baseName = project.name
    into "$baseName-$version"
    with executablesSpec
}

task distSrc(type: Zip) {
    baseName = project.name
    classifier = 'src'
    into "$baseName-$version"
    with srcSpec
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.3'
}

